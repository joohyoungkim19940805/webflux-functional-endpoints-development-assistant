//build.gradle
plugins {
    id 'java-library'                               // 라이브러리용
    id 'io.spring.dependency-management' version '1.1.7'
    id "com.vanniktech.maven.publish" version "0.36.0"
}

group = 'com.byeolnaerim'
version = '0.0.1-alpha.1'
jar {
    archiveBaseName = 'webflux-fe-dev-assistant'
}
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
  	//withJavadocJar()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
  mavenCentral()
  maven { url = uri('https://repo.spring.io/milestone') } 
  maven { url = uri('https://repo.spring.io/snapshot') } 
}

dependencies {    
	
	implementation platform("org.springframework.boot:spring-boot-dependencies:4.0.0")
	
	compileOnly 'org.springframework:spring-webflux'
	compileOnly 'io.projectreactor:reactor-core'
	
	implementation 'tools.jackson.core:jackson-databind'
	
	implementation 'com.github.javaparser:javaparser-core:3.28.0'
	implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.28.0'
    
    implementation 'fr.inria.gforge.spoon:spoon-core:11.3.1-beta-7'
    
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    
    compileOnly 'org.jspecify:jspecify:1.0.0'
}

def isCentralPublish = gradle.startParameter.taskNames.any { t ->
	t.toLowerCase().contains("mavencentral")
}

tasks.withType(JavaExec) {
    jvmArgs += '--add-exports'
    jvmArgs += 'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED'
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-parameters']
  	options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
  // 윈도우에서 한글/인코딩 깨짐 방지
  options.encoding = 'UTF-8'
  options.charSet = 'UTF-8'
  options.docEncoding = 'UTF-8'

  // DocLint(HTML/태그 엄격검사) 완화
  options.addStringOption('Xdoclint:none', '-quiet')

  // Javadoc 에러로 빌드 전체가 죽지 않게
  failOnError = false
}

import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.SourcesJar

mavenPublishing {
	configureBasedOnAppliedPlugins(
		new JavadocJar.Empty(),
		new SourcesJar.Sources()
	)
  
    // Central Portal 배포 타겟 + 서명 (Central 요구사항)
    publishToMavenCentral()     // 자동 릴리즈까지 하려면 publishToMavenCentral(true)
	if (isCentralPublish) {
		signAllPublications()
	}

    // 좌표(GAV)
    coordinates(project.group, 'webflux-fe-dev-assistant', project.version)

    // Central 요구 POM 메타데이터(최소치라도 채우는 게 안전)
    pom {
        name = 'WebFlux Functional Endpoints Development Assistant'
        description = 'Spring WebFlux functional endpoint 개발 보조 라이브러리'
        url = 'https://github.com/joohyoungkim19940805/webflux-functional-endpoints-development-assistant'

        licenses {
            license {
                name = 'The Apache License, Version 2.0'
                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'joohyoungkim19940805'
                name = '김주형'
                email = 'oozu1994@gmail.com'
                url = 'https://github.com/joohyoungkim19940805'
            }
        }
        scm {
            url = 'https://github.com/joohyoungkim19940805/webflux-functional-endpoints-development-assistant'
            connection = 'scm:git:git://github.com/joohyoungkim19940805/webflux-functional-endpoints-development-assistant.git'
            developerConnection = 'scm:git:ssh://git@github.com/joohyoungkim19940805/webflux-functional-endpoints-development-assistant.git'
        }
    }
}

import org.gradle.api.publish.tasks.GenerateModuleMetadata

tasks.withType(GenerateModuleMetadata).configureEach {
    // publication 메타데이터 생성 전에 emptyJavadocJar 산출물을 확실히 만들게 함
    dependsOn(tasks.named("emptyJavadocJar"))
}
