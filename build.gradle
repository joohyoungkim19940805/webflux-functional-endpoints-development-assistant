//build.gradle
plugins {
    id 'java-library'                               // 라이브러리용
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'                              // 배포용
}

group = 'com.byeolnaerim'
version = '0.0.1-SNAPSHOT' //-alpha.1
jar {
    archiveBaseName = 'webflux-fe-dev-assistant'
}
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
  mavenCentral()
  maven { url = uri('https://repo.spring.io/milestone') } 
  maven { url = uri('https://repo.spring.io/snapshot') } 
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.0"
    }
}
dependencies {    
	api 'org.springframework.data:spring-data-mongodb'
	api 'org.springframework:spring-webflux'
	api 'io.projectreactor:reactor-core'
	
	implementation 'tools.jackson.core:jackson-databind'
	
	implementation 'com.github.javaparser:javaparser-core:3.27.1'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.27.1'
    
    implementation 'fr.inria.gforge.spoon:spoon-core:11.2.1'
    
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:4.21.0'
    
    compileOnly 'org.jspecify:jspecify:1.0.0'
}

tasks.withType(JavaExec) {
    jvmArgs += '--add-exports'
    jvmArgs += 'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED'
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs += ['-parameters']
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java  // java-library에서 만들어주는 컴포넌트

            groupId = project.group
            artifactId = 'webflux-fe-dev-assistant'
            version = project.version

            // (나중에 Maven Central 갈 거면 pom 메타데이터를 더 채우면 좋음)
            pom {
                name = 'WebFlux Functional Endpoints Development Assistant'
                description = 'Spring WebFlux functional endpoint 개발 보조 라이브러리'
            }
        }
    }
    // repositories {} 는 로컬 테스트만 할 거면 아직 안 넣어도 됨 (mavenLocal은 자동)
}